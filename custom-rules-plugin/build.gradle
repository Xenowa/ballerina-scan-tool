/*
 * Copyright (c) 2024, WSO2 LLC. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    // For using the ballerina java project API
    id 'java-library'
    // For checkstyle configurations
    id 'checkstyle'
    // For spotbugs configurations
    id "com.github.spotbugs" version "5.0.14"
    // For providing file download tasks functionality of checkstyle files
    id "de.undercouch.download" version "5.4.0"
    // Required plugin to create an executable jar which does not require external dependencies
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

group = 'org.arc.scanner'
version = '1.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://repox.jfrog.io/repox/plugins.gradle.org/'
    }

    // Required repository for testing
    maven {
        url = uri("https://plugins.gradle.org/m2/")
    }

    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }

    // For Ballerina Plugins
    maven {
        url = 'https://maven.pkg.github.com/ballerina-platform/*'
        credentials {
            username System.getenv("packageUser")
            password System.getenv("packagePAT")
        }
    }
}

dependencies {
    // ===========================
    // For Ballerina Project API's
    // ===========================
    // set the correct gson version
    implementation "com.google.code.gson:gson:${project.gsonVersion}"

    // Required dependencies for accessing the ballerina java project API
    // The version should match the version you have in the local machine
    implementation group: 'org.ballerinalang', name: 'ballerina-lang', version: "${project.ballerinaVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-parser', version: "${project.ballerinaVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-runtime', version: "${project.ballerinaVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-tools-api', version: "${project.ballerinaVersion}"
    implementation group: 'org.ballerinalang', name: 'jballerina-tools', version: "${project.ballerinaVersion}"
    // Required dependencies for building a ballerina cli application
    implementation group: 'org.ballerinalang', name: 'ballerina-cli', version: "${project.ballerinaVersion}"
    // Required Module to convert the AST to a JSON File
    implementation group: 'org.ballerinalang', name: 'diagram-util', version: "${project.ballerinaVersion}"

    // Required for determining the platform a java process is running on
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'

    // Required for applying checkstyle & Spotbugs to the project
    checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: '10.12.1'

    // Adding dependency to the bal scan tool to retrieve the interface methods
    implementation project(path: ':ScanCommand')
}

// Setting up the ballerina home property for the project
tasks.withType(JavaExec).configureEach {
    systemProperty 'ballerina.home', System.getenv("BALLERINA_HOME")
}

// =====================
// Setting up checkstyle
// =====================
// Downloading checkstyle configurations of WSO2
task downloadCheckstyleRuleFiles(type: Download) {
    src([
            'https://raw.githubusercontent.com/wso2/code-quality-tools/v1.4/checkstyle/jdk-17/checkstyle.xml',
            'https://raw.githubusercontent.com/wso2/code-quality-tools/v1.4/checkstyle/jdk-17/suppressions.xml'
    ])
    overwrite false
    onlyIfNewer true
    dest buildDir
}

// Location for placing the wso2 checkstyle configuration
artifacts.add('default', file("${project.buildDir}/checkstyle.xml")) {
    builtBy(downloadCheckstyleRuleFiles)
}

// Location for placing the suppression files configurations
artifacts.add('default', file("${project.buildDir}/suppressions.xml")) {
    builtBy(downloadCheckstyleRuleFiles)
}

// Files to be excluded from checkstyle \
def excludePattern = '**/module-info.java'
tasks.withType(Checkstyle) {
    exclude excludePattern
}

// Checkstyle outputs locations
checkstyle {
    toolVersion "10.12.1"
    // As in the current implementation the project is as a sub module
    configFile rootProject.file("${project.buildDir}/checkstyle.xml")
    configProperties = ["suppressionFile": file("${project.buildDir}/suppressions.xml")]
}

checkstyleTest {
    enabled = true
}

// Run checkstyle only after downloading the custom checkstyle configurations
checkstyleMain.dependsOn(downloadCheckstyleRuleFiles)
checkstyleTest.dependsOn(downloadCheckstyleRuleFiles)

// ===================
// Setting up spotbugs
// ===================
spotbugsMain {
    effort "max"
    reportLevel "low"

    // Spotbugs report destination
    reportsDir = file("$project.buildDir/reports/spotbugs")

    // Spotbugs report types to generate
    reports {
        html.enabled true
        text.enabled = true
    }

    // spotbugs exclusions file destination
    def excludeFile = file("${projectDir}/spotbugs-exclude.xml")
    if (excludeFile.exists()) {
        excludeFilter = excludeFile
    }
}

// Have spotbugs test enabled
spotbugsTest {
    effort = "max"
    reportLevel = "low"

    // Spotbugs report destination
    reportsDir = file("$project.buildDir/reports/spotbugs")

    // Spotbugs report types to generate
    reports {
        html.enabled true
        text.enabled = true
    }

    // spotbugs exclusions file destination
    def excludeFile = file("${projectDir}/spotbugs-exclude.xml")
    if (excludeFile.exists()) {
        excludeFilter = excludeFile
    }
}

task validateSpotbugs() {
    doLast {
        if (spotbugsMain.reports.size() > 0 &&
                spotbugsMain.reports[0].destination.exists() &&
                spotbugsMain.reports[0].destination.text.readLines().size() > 0) {
            spotbugsMain.reports[0].destination?.eachLine {
                println 'Failure: ' + it
            }
        } else {
            throw new GradleException("Spotbugs rule violations were found.");
        }
    }
}

spotbugsMain.finalizedBy validateSpotbugs

// ===========================
// JAR creation configurations
// ===========================
// Configurations for the output jar file when the gradle build is performed
// Configure global properties
ext {
    set("buildNumber", System.getProperty("buildNumber"))
}

tasks.jar {
    manifest {
        def displayVersion = (project.property("buildNumber") == null ? project.version : project.version.substring(0, project.version.lastIndexOf('.')) + " (build ${project.property("buildNumber")})")
        def buildDate = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
}

// Configuration required to create the shadow jar file
def shadowJar = tasks.shadowJar

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.shadowJar {
    minimize {}

    // build related files, useless for the plugin
    exclude '**/*.js'
    exclude '**/*.properties'
    exclude '**/*.proto'
    exclude '**/*.txt'

    // Other unwanted exclusions
    exclude "cli-help/**"
    exclude "com/**"
    exclude "create_cmd_templates/**"
    exclude "doc-ui/**"
    exclude "io/ballerina/compiler/**"
    exclude "io/ballerina/identifier/**"
    exclude "io/ballerina/projects/**"
    exclude "io/ballerina/runtime/**"
    exclude "io/ballerina/toml/**"
    exclude "io/ballerina/tools/**"
    exclude "io/netty/**"
    exclude "io/opentelemetry/**"
    exclude "javax/**"
    exclude "kotlin/**"
    exclude "licenses/**"
    exclude "META-INF/maven/**"
    exclude "META-INF/native-image/**"
    exclude "META-INF/native/**"
    exclude "META-INF/proguard/**"
    exclude "META-INF/resources/**"
    exclude "META-INF/services/org/**"
    exclude "META-INF/services/com.atomikos.icatch.provider.Assembler"
    exclude "META-INF/services/com.atomikos.icatch.TransactionServicePlugin"
    exclude "META-INF/services/com.ctc.wstx.shaded.msv.relaxng_datatype.DatatypeLibraryFactory"
    exclude "META-INF/services/io.ballerina.cli.BLauncherCmd"
    exclude "META-INF/services/io.ballerina.cli.launcher.BLauncherCmd"
    exclude "META-INF/services/io.ballerina.runtime.internal.services.spi.EmbeddedExecutor"
    exclude "META-INF/services/javax.annotation.processing.Processor"
    exclude "META-INF/services/javax.xml.stream.XMLEventFactory"
    exclude "META-INF/services/javax.xml.stream.XMLInputFactory"
    exclude "META-INF/services/javax.xml.stream.XMLOutputFactory"
    exclude "META-INF/services/org.ballerinalang.compiler.plugins.CompilerPlugin"
    exclude "META-INF/services/org.codehaus.stax2.validation.XMLValidationSchemaFactory"
    exclude "META-INF/services/org.codehaus.stax2.validation.XMLValidationSchemaFactory.dtd"
    exclude "META-INF/services/org.codehaus.stax2.validation.XMLValidationSchemaFactory.relaxng"
    exclude "META-INF/services/org.codehaus.stax2.validation.XMLValidationSchemaFactory.w3c"
    exclude "META-INF/services/org.testng.xml.ISuiteParser"
    exclude "META-INF/services/reactor.blockhound.integration.BlockHoundIntegration"
    exclude "META-INF/sisu/**"
    exclude "META-INF/versions/**"
    exclude "META-INF/axiom.xml"
    exclude "META-INF/DEPENDENCIES"
    exclude "META-INF/kotlin-stdlib.kotlin_module"
    exclude "META-INF/kotlin-stdlib-common.kotlin_module"
    exclude "META-INF/kotlin-stdlib-jdk7.kotlin_module"
    exclude "META-INF/kotlin-stdlib-jdk8.kotlin_module"
    exclude "META-INF/LICENSE"
    exclude "META-INF/LICENSE.md"
    exclude "META-INF/NOTICE"
    exclude "META-INF/NOTICE.md"
    exclude "META-INF/okio.kotlin_module"
    exclude "META-INF/properties"
    exclude "me/**"
    exclude "new_cmd_defaults/**"
    exclude "okhttp3/**"
    exclude "okio/**"
    exclude "org/apache/**"
    exclude "org/ballerinalang/**"
    exclude "org/codehaus/**"
    exclude "org/eclipse/**"
    exclude "org/jacoco/**"
    exclude "org/jetbrains/**"
    exclude "org/jline/**"
    exclude "org/objectweb/**"
    exclude "org/slf4j/**"
    exclude "org/sonatype/**"
    exclude "org/testng/**"
    exclude "org/wso2/ballerinalang/**"
    exclude "about.html"
    exclude "bal-tools-toml-schema.json"
    exclude "ballerina-toml-schema.json"
    exclude "dependencies-toml-schema.json"
    exclude "jballerina-tools-2201.8.5.zip"
    exclude "old-dependencies-toml-schema.json"
    exclude "plugin.xml"
    exclude "production.html"
    exclude "release-description.md"
    exclude "settings-toml-schema.json"
    exclude "syntax_tree_descriptor.json"
    exclude "template.declaration.mustache"
    exclude "template.execution.mustache"
    exclude "testng.css"
    exclude "testngtasks"
    exclude "ballerina-scan.help"
    exclude "report.zip"
    exclude "testng-1.0.dtd"
}

artifacts {
    archives shadowJar
}

// Configurations to automatically build and deploy custom static code analyzer compiler plugin
def packageName = "custom_static_code_analyzer"
def tomlVersion = "${project.pluginVersion}"
def ballerinaTomlFilePlaceHolder = new File("${projectDir}/custom-static-code-analyzer/Ballerina.toml")
def compilerPluginTomlFilePlaceHolder = new File("${projectDir}/custom-static-code-analyzer/CompilerPlugin.toml")
def ballerinaTomlFile = new File("${projectDir}/custom-static-code-analyzer/Ballerina.toml")
def compilerPluginTomlFile = new File("${projectDir}/custom-static-code-analyzer/CompilerPlugin.toml")
def pluginCentralDirectory = System.getProperty("user.home") + "/.ballerina/repositories/central.ballerina.io/bala/tharana_wanigaratne"
def pluginLocalDir = System.getProperty("user.home") + "/.ballerina/repositories/local/bala/tharana_wanigaratne"
def balCentralCacheDir = project.file(System.getProperty("user.home") + "/.ballerina/repositories/central.ballerina.io")

task updateTomlFiles {
    // Update the Ballerina.toml and Dependencies.toml files
    doLast {
        def newConfig = ballerinaTomlFilePlaceHolder.text.replace("@project.pluginVersion@", project.pluginVersion)
        newConfig = newConfig.replace("@toml.version@", tomlVersion)
        ballerinaTomlFile.text = newConfig

        def newToolConfig = compilerPluginTomlFilePlaceHolder.text.replace("@project.pluginVersion@", project.pluginVersion)
        newToolConfig = newToolConfig.replace("@toml.version@", tomlVersion)
        compilerPluginTomlFile.text = newToolConfig
    }
}

task buildCompilerPlugin {
    doLast {
        // Pack and push to local repo of distribution
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', 'cd custom-static-code-analyzer & bal pack & bal push --repository=local'
            } else {
                commandLine 'sh', '-c', 'cd custom-static-code-analyzer ; bal pack ; bal push --repository=local'
            }
        }
        println("Successfully build and pushed the custom static code analyzer compiler plugin to the local repository")

        // Remove the cache directories in the central repository
        delete {
            fileTree(balCentralCacheDir).matching {
                include 'cache-*'
            }
        }
        println("Successfully cleaned the .ballerina/cache* directories")

        // Update the central repository
        def balDestinationDir = "$pluginCentralDirectory/$packageName"
        def balSourceDir = "$pluginLocalDir/$packageName"
        if (file(balDestinationDir).exists()) {
            file(balDestinationDir).deleteDir()
        }
        copy {
            from balSourceDir
            into balDestinationDir
        }
        println("Successfully copied package from local/bala the central.ballerina.io/bala directory")
    }
}

build {
    dependsOn updateTomlFiles
    dependsOn buildCompilerPlugin
}

// To make sure the compiler plugins gets built after the fat JAR is created
buildCompilerPlugin.dependsOn shadowJar